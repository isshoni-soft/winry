plugins {
    id 'java-library'
    id 'maven-publish'
    id 'koyomi-araragi' version '1.1.4'
}

repositories {
    mavenLocal()
    mavenCentral()
    isshoni()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    compileOnly "javax.xml.bind:jaxb-api:$jaxb_version"

    api "tv.isshoni.araragi:core:$araragi_version"
    api "tv.isshoni.araragi:annotation:$araragi_version"
    api "tv.isshoni.araragi:logging:$araragi_version"
    api "com.google.code.gson:gson:$gson_version"

    implementation "net.oneandone.reflections8:reflections8:$reflections8_version"
    implementation "net.bytebuddy:byte-buddy:$bytebuddy_version"
    implementation "net.bytebuddy:byte-buddy-agent:$bytebuddy_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.powermock:powermock-module-junit4:$powermock_version"
}

group = 'tv.isshoni'
version = "$winry_version"

test {
    environment "TEST", "true"
}

processResources {
    filesMatching("**/winry_version.properties") {
        expand(
            'version': project.version,
        )
    }
}

publishing {
    repositories {
        isshoni()
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            from components.java
        }
    }
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

compileJava.options.compilerArgs.add '-parameters'
compileTestJava.options.compilerArgs.add '-parameters'
